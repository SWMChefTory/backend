plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '4.0.4'
	id 'jacoco'
	id "io.sentry.jvm.gradle" version "5.9.0"
	id 'com.diffplug.spotless' version '6.25.0'
}

spotless {
	java {
		target '**/*.java'
		palantirJavaFormat('2.50.0')
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
		targetExclude 'src/main/resources/static/docs/**'
	}
}

group = 'com.cheftory'
version = '1.1.25'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
	toolVersion = "0.8.13"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

def isProd = System.getenv('SPRING_PROFILES_ACTIVE') == 'prod'

if (isProd) {
	sentry {
		includeSourceContext = true
		org = "cheftory"
		projectName = "cheftory-backend"
		authToken = System.getenv("SENTRY_AUTH_TOKEN")
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'
	implementation "org.openapitools:jackson-databind-nullable:0.2.6"

	implementation(platform("io.sentry:sentry-bom:8.20.0"))
	implementation("io.sentry:sentry-spring-boot-starter-jakarta")

	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "org.xmlunit", module: "xmlunit-core"
		exclude group: "org.skyscreamer", module: "jsonassert"
		exclude group: "junit", module: "junit"
		exclude group: "org.junit.vintage", module: "junit-vintage-engine"
		exclude group: "org.hamcrest", module: "hamcrest"
	}
	testImplementation 'org.springframework.security:spring-security-test'

	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation "org.springframework.retry:spring-retry"
	implementation "org.springframework:spring-aspects"
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'io.rest-assured:spring-mock-mvc'
	testImplementation("com.squareup.okhttp3:mockwebserver3-junit5:5.3.0") {
		exclude group: "junit", module: "junit"
		exclude group: "org.hamcrest", module: "hamcrest"
	}
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly  'io.micrometer:micrometer-registry-prometheus'

	implementation("org.opensearch.client:spring-data-opensearch-starter:2.0.2") {
		exclude group: "org.opensearch.client", module: "opensearch-rest-high-level-client"
	}
	implementation("org.opensearch.client:opensearch-java:3.1.0")

	implementation 'org.springframework.boot:spring-boot-starter-batch'
	testImplementation 'org.springframework.batch:spring-batch-test'

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	testImplementation 'com.github.codemonstur:embedded-redis:1.4.3'
}

def snippetsDir = file("build/generated-snippets")

tasks {
	test {
		useJUnitPlatform()

		doFirst {
			delete("build/generated-snippets")
		}
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.required = true
			html.required = true
			csv.required = true
		}

		afterEvaluate {
			classDirectories.setFrom(files(classDirectories.files.collect {
				fileTree(dir: it, exclude: [
						'**/_*/**',
						'**/*Application*',
						'**/*ErrorCode.class',
						'**/*Exception.class',
						'**/*Sort.class',
						'**/*PageRequest.class',
				])
			}))
		}
	}


	asciidoctor {

		inputs.dir snippetsDir


		dependsOn test

		baseDirFollowsSourceDir()

		attributes 'snippets': snippetsDir.absolutePath
	}

	bootJar {
		dependsOn asciidoctor
		from("build/docs/asciidoc") {
			into 'static/docs'
		}
	}

	tasks.register('copyAsciidoctor', Copy) {
		dependsOn asciidoctor

		from(layout.buildDirectory.dir("docs/asciidoc"))
		into layout.projectDirectory.dir('src/main/resources/static/docs')
	}

	build {
		dependsOn copyAsciidoctor
	}
}
